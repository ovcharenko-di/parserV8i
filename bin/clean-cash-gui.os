#Использовать ".."

Перем Форма;
Перем Парсер;
Перем Чистильщик;
Перем ПолеСоСпискомБаз;

Процедура Инициализация()
    Попытка
    	ПодключитьВнешнююКомпоненту("oscript-simple-gui.dll");    
    Исключение
        ПодключитьВнешнююКомпоненту("bin/oscript-simple-gui.dll");
    КонецПопытки;
    

	Парсер = Новый ПарсерСпискаБаз;

	Чистильщик = Новый ОчисткаКеша();
	Чистильщик.УстановитьПарсер(Парсер);

    ПолеСоСпискомБаз = Неопределено;
    
КонецПроцедуры

//# Работа с событиями
Процедура ПоКнопкеОчиститьВесьКеш() Экспорт
    Чистильщик.ОчиститьВесьКеш();
КонецПроцедуры

Процедура ПоКнопкеОчиститьКешБазы() Экспорт
    База = Парсер.НайтиПоИмени(ПолеСоСпискомБаз.Значение);
    Если База <> Неопределено Тогда
       Чистильщик.ОчиститьКеш(База); 
    КонецЕсли;
КонецПроцедуры

Процедура ПриОткрытииФормы() Экспорт

	Страницы = Форма.Элементы.Добавить("Страницы1", "ГруппаФормы", Неопределено);
	Страницы.Вид = Форма.ВидГруппыФормы.Страницы;
	Страницы.Заголовок = "Страницы";

	//#Region ОчиститьВесьКеш 
	СтраницаОчиститьВесьКеш = Форма.Элементы.Добавить("СтраницаОчиститьВесьКеш", "ГруппаФормы", Страницы);
	СтраницаОчиститьВесьКеш.Вид = Форма.ВидГруппыФормы.Страница;
	СтраницаОчиститьВесьКеш.Заголовок = "Очистить весь кеш";

    КнопкаОчиститьВесьКеш = Форма.Элементы.Добавить("КнопкаОчиститьВесьКеш", "КнопкаФормы", СтраницаОчиститьВесьКеш);
    КнопкаОчиститьВесьКеш.Заголовок = "Очистить весь кеш";
    КнопкаОчиститьВесьКеш.УстановитьДействие(ЭтотОбъект, "Нажатие", "ПоКнопкеОчиститьВесьКеш");
	//#EndRegion ОчиститьВесьКеш

	//#Region ВыборочнаяОчисткаКеша 
	СтраницаОчиститьКешВыборочно = Форма.Элементы.Добавить("СтраницаОчиститьКешВыборочно", "ГруппаФормы", Страницы);
	СтраницаОчиститьКешВыборочно.Вид = Форма.ВидГруппыФормы.Страница;
	СтраницаОчиститьКешВыборочно.Заголовок = "Очистить кеш выбранной базы";

	Данные = Новый Соответствие();
	
    СписокБаз = Парсер.ПолучитьСписокБаз();
	Для каждого База Из СписокБаз Цикл
		СтруктураАдреса = База.Значение;
        Данные.Вставить(СтруктураАдреса.Name, СтруктураАдреса.Name);
	КонецЦикла;

	ПолеСоСпискомБаз = Форма.Элементы.Добавить("ПолеСоСпискомБаз", "ПолеФормы", СтраницаОчиститьКешВыборочно);
	ПолеСоСпискомБаз.Вид = Форма.ВидПоляФормы.ПолеСоСписком;
    ПолеСоСпискомБаз.Заголовок = "База:";
	ПолеСоСпискомБаз.СписокВыбора = Данные;

	КнопкаОчиститьКешБазы = Форма.Элементы.Добавить("КнопкаОчиститьКешБазы", "КнопкаФормы", СтраницаОчиститьКешВыборочно);
    КнопкаОчиститьКешБазы.Заголовок = "Очистить кеш выбранной базы";
	КнопкаОчиститьКешБазы.УстановитьДействие(ЭтотОбъект, "Нажатие", "ПоКнопкеОчиститьКешБазы");

	//#EndRegion ВыборочнаяОчисткаКеша

    СтраницаОчиститьКешВыборочно.Высота = 50;
    СтраницаОчиститьКешВыборочно.Ширина = 600;
    Форма.Высота = СтраницаОчиститьКешВыборочно.Высота+50;
    Страницы.Закрепление = 5;

КонецПроцедуры

Процедура СоздатьФорму()
	ПростойГУИ = Новый ПростойГУИ();
	Форма = ПростойГУИ.СоздатьФорму();
	Форма.УстановитьДействие(ЭтотОбъект, "ПриОткрытии", "ПриОткрытииФормы");

    Форма.Показать();	
КонецПроцедуры

Инициализация(); 
СоздатьФорму();